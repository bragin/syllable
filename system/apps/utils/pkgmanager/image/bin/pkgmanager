#! /usr/bin/env orca

ORCA [
	Title:		"Package registration tool"
	Version:	0.1.0
	Author:		"Kaj de Vos"
	Rights: {
		Copyright (c) 2006 Kaj de Vos
		License: GPL 2 or any later version
	}
	Purpose:	"Registers and unregisters binary packages by managing symlinks in the system."
	Example:	"pkgmanager add /usr/example"

	Tabs:		4
]


log: context [  ; Singleton logger object
	open: func [] [
	]
	close: does [
	]

	header: func [header] [
		prin newline
		print header
	]
	detail: func [detail] [
;		print detail
	]
	action: func [action] [
;		prin newline
;		print action
;		prin newline
	]
	output: func [output] [
	]
	error: func [error] [
		prin newline
		print ["Error:" error]
	]
	warning: func [warning] [
		prin newline
		print ["Warning:" warning]
	]
	failure: func [module] [
		prin newline
		print ["Package" module "failed!"]
	]
]

action: function ["Returns the output of a shell command, or false if unsuccessful"
	command "Shell command"
][
	status
][
	log/action command

	either (status: call/wait/output command stdout: "") = 0 [
		log/output stdout
		stdout
	][
		log/error status
		log/output stdout
		no
	]
]

build: context [
	system?: no
]

stage-proto: context [
;	packages-root: indexes: none
	indexes: %/atheos/autolnk/
	
	index-node: function [package node] [
		source destination
	][
		recycle  ; Orca bug

;		source: join packages-root [package "/" node]
		source: join package ["/" node]
		destination: join indexes node

		either dir? source [
			either exists? destination [
;				if link? destination [log/warning join "directory " [destination " is a link"]]
				yes
			][
				if dir? destination [log/warning join "replacing broken directory link " destination]
				action join "mkdir " destination
			]
		][
			either exists? destination [
				either node = "info/dir" [
					log/warning "info/dir file not registered due to pre-existing file"
					yes
				][
					log/error join "conflict: link " [destination " already exists!"]
					no
				]
			][
;				Bug in Orca when path given:
;				if file? destination [log/warning join "replacing broken link " destination]
				action join "ln -s " [source " " destination]
			]
		]
	]
	
	remove-node: function [subdir node] [
		index file
	][
		recycle  ; Orca bug

		index: join indexes [file: join subdir ["/" node]]

		either dir? index [
			any [
;				yes  ; Bug in Orca 0.0.23 list comparison:
				(read index) <> []  ; Indexes directory empty?
				action join "rmdir " index
			]
		][
;			Orca join bug:
;			change-dir join indexes [subdir]
;			Orca bug:
;			either file? node [
			either exists? index [
				either file = "info/dir" [
					log/warning "info/dir file left untouched"
					yes
				][
					action join "rm " index
				]
			][
				log/warning join "link " [index " did not exist"]
				yes
			]
		]
	]
]

system-image: make stage-proto [
	type: none
	root: %/
	packages-root: %/usr/
	indexes: %/atheos/autolnk/
]

stage: make stage-proto [
	type: none
	root: none
]

package-proto: context [
	package: stage: none

	flat-dirs:	 [etc bin sbin info early-init init]
	nested-dirs: [lib libexec include share man]

	new: func [package' stage'] [
		make self [package: to-file package' stage: stage']
	]

	register: function [stage] [
		path
	][
		log/header join "Registering " package

		either all [
			do-subdirs :index-subdir reduce [system-image]
			either exists? path: join package "/man" [
				log/header "Generating manual pages"
				action join "manmanager -a " path
			][yes]
		][
			action "sync"
		][
			log/failure package
			no
		]
	]

	unregister: function [stage] [
		path
	][
		log/header join "Unregistering " package

		either all [
			do-subdirs :remove-subdir reduce [system-image]
;			either no [  ; manmanager fails on deleting non-existing manuals
			either exists? path: join package "/man" [
				log/header "Deleting manual pages"
				action join "manmanager -r " path
			][yes]
		][
			action "sync"
		][
			log/failure package
			no
		]
	]


	do-subdirs: function [task stages] [
		subdir
	][
		recycle  ; Orca bug

		foreach subdir flat-dirs [
			unless task stages subdir no
				[return no]
		]
		foreach subdir nested-dirs [
			unless task stages subdir yes
				[return no]
		]
	]
	
	index-subdir: function [stages subdir nesting?] [
		dir entry node s
	][
;		either exists? dir: join stage/packages-root [package "/" subdir] [
		either exists? dir: join package ["/" subdir] [
			log/detail join "Processing subdirectory " subdir
			change-dir dir

			foreach entry read %. [
				node: join subdir ["/" entry]
				foreach s stages [
					unless s/index-node package node
						[return no]
				]
				if nesting? and dir? entry [
					unless index-subdir stages node yes
						[return no]
					change-dir dir
				]
			]
			yes
		][yes]
	]
	
	remove-subdir: function [stages subdir nesting?] [
		dir entry s
	][
;		either exists? dir: join stage/packages-root [package "/" subdir] [
		either exists? dir: join package ["/" subdir] [
			log/detail join "Processing subdirectory " subdir
			change-dir dir

			foreach entry read %. [
				if nesting? and dir? entry [
					unless remove-subdir stages join subdir ["/" entry] yes
						[return no]
				]
				foreach s stages [
					unless s/remove-node subdir entry
						[return no]
				]
				change-dir dir
			]
			yes
		][yes]
	]
]


header: system/script/header

either any [
	not args: system/script/args
	(
		command: args/1
		package: args/2

		find ["help" "-h" "--help" "-help" "-?" "?"] command
	)
][
	print "Usage: pkgmanager <command> [<package>]"
	print "<command>:"
	print "  help, -h,     Show this help information"
	print "    --help, -help, -?, (?)"
	print "  version, -v   Show version information"
	print "  add, -a       Register a package"
	print "  remove, -r    Unregister a package"
	print "Example:"
	print [" " select header 'example]
][
either find ["version" "-v" "--version"] command [
	print [select header 'title select header 'version]
	print select header 'rights
][
either find ["add" "-a" "--add"] command [
	package: package-proto/new package system-image
	package/register none
][
either find ["remove" "-r" "--remove"] command [
	package: package-proto/new package system-image
	package/unregister none
][
	print "Unknown command"
]]]]

; FIXME: no errors returned

